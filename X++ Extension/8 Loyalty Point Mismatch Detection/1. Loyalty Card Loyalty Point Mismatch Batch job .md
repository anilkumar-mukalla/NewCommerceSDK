```cs
public class EYSLoyaltyCardPointsMisMatchBatchJob  extends RunBaseBatch  implements BatchRetryable
{
    RetailLoyaltyCardNumber loyaltyCardNumber;
    RetailLoyaltyCard       retailLoyaltyCard;
    Query                   query;
    QueryBuildDataSource    qbdsRetailLoyaltyCard, qbdsRewardPoint, qbdsSchemeRewardPoint, qbdsScheme, qbdsLoyaltyGroup, qbdsCardTier;
    DialogField             dialogloyaltyCardNumber;
    str                     csvFileName;

    #define.CurrentVersion(1)
    #localmacro.CurrentList
    loyaltyCardNumber
    #endmacro
    
    public RetailLoyaltyCardNumber parmLoyaltyCardNumber(RetailLoyaltyCardNumber _retailLoyaltyCardNumber = loyaltyCardNumber)
    {
        this.loyaltyCardNumber = _retailLoyaltyCardNumber;
        return this.loyaltyCardNumber;
    }

    public boolean canGoBatchJournal()
    {
        return true;
    }

    public boolean init()
    {
        super();

        csvFileName = this.generateFileName();

        query = new Query();

        // Add data sources to RetailLoyaltyRewardPoint in order to filter reward points by loyalty card
        qbdsRewardPoint = query.addDataSource(tableNum(RetailLoyaltyRewardPoint));

        qbdsSchemeRewardPoint = qbdsRewardPoint.addDataSource(TableNum(RetailLoyaltySchemeRewardPointView), tableStr(RetailLoyaltySchemeRewardPointView));
        qbdsSchemeRewardPoint.joinMode(JoinMode::ExistsJoin);
        qbdsSchemeRewardPoint.addLink(fieldNum(RetailLoyaltyRewardPoint, RecId), fieldNum(RetailLoyaltySchemeRewardPointView, RewardPoint));

        qbdsScheme = qbdsSchemeRewardPoint.addDataSource(TableNum(RetailLoyaltyScheme), tableStr(RetailLoyaltyScheme));
        qbdsScheme.joinMode(JoinMode::ExistsJoin);
        qbdsScheme.addLink(fieldNum(RetailLoyaltySchemeRewardPointView, LoyaltyScheme), fieldNum(RetailLoyaltyScheme, RecId));

        qbdsLoyaltyGroup = qbdsScheme.addDataSource(TableNum(RetailAffiliation), tableStr(RetailAffiliation));
        qbdsLoyaltyGroup.joinMode(JoinMode::ExistsJoin);
        qbdsLoyaltyGroup.addLink(fieldNum(RetailLoyaltyScheme, Affiliation), fieldNum(RetailAffiliation, RecId));

        qbdsCardTier = qbdsLoyaltyGroup.addDataSource(TableNum(RetailLoyaltyCardTier), tableStr(RetailLoyaltyCardTier));
        qbdsCardTier.joinMode(JoinMode::ExistsJoin);
        qbdsCardTier.addLink(fieldNum(RetailAffiliation, RecId), fieldNum(RetailLoyaltyCardTier, Affiliation));
          
        return true;
    }

    protected void new()
    {
        this.init();

        super();
    }

    public container pack()
    {
        return [#CurrentVersion,#CurrentList];
    }

    public boolean runsImpersonated()
    {
        return true;
    }

    public boolean unpack(container packedClass)
    {
        Version version = RunBase::getVersion(packedClass);
        ;
        switch (version)
        {
            case #CurrentVersion:
                [version, #CurrentList] = packedClass;
                break;
            default:
                return false;
        }

        return true;
    }

    public static EYSLoyaltyCardPointsMisMatchBatchJob construct()
    {
        return new EYSLoyaltyCardPointsMisMatchBatchJob();
    }

    public boolean mustGoBatch()
    {
        return false;
    }

    public ClassDescription caption()
    {
        return "EYS Loyalty Card Adjustment Point Batch Job";
    }

    [Hookable(false)]
    public final boolean isRetryable()
    {
        return true;
    }

    protected boolean canRunInNewSession()
    {
        return false;
    }

    public Object dialog()
    {
        DialogRunbase       dialog = super();
        #resAppl;

        dialogloyaltyCardNumber = dialog.addFieldValue(extendedTypeStr(RetailLoyaltyCardNumber), loyaltyCardNumber);

        return dialog;
    }

    public boolean getFromDialog()
    {
        loyaltyCardNumber     = dialogloyaltyCardNumber.value();

        return super();
    }

    public void run()
    {
        #OCCRetryCount
        try
        {
            TextStreamIo textStreamIO = TextStreamIo::constructForWrite();
            str _line = strFmt('%1,%2\n', 'loyaltyCardNumber', 'retailLoyaltyRewardPointID');
           
            if (loyaltyCardNumber)
            {
                retailLoyaltyCard = RetailLoyaltyCard::findByCardNumber(loyaltyCardNumber);
                qbdsCardTier.clearRanges();
                qbdsCardTier.addRange(fieldNum(RetailLoyaltyCardTier, LoyaltyCard)).value(queryValue(RetailLoyaltyCard.RecId));

                QueryRun queryRun = new QueryRun(query);
                while(queryRun.next())
                {
                    RetailLoyaltyRewardPoint  retailLoyaltyRewardPoint = queryRun.get(tableNum(RetailLoyaltyRewardPoint));
                    RetailLoyaltyRewardPointAmountQty issuedPoints = this.IssuedPoints(retailLoyaltyRewardPoint);
                    RetailLoyaltyRewardPointAmountQty usedPoints  = this.UsedPoints(retailLoyaltyRewardPoint);
                    RetailLoyaltyRewardPointAmountQty expiredPoints = this.ExpiredPoints(retailLoyaltyRewardPoint);
                    RetailLoyaltyRewardPointAmountQty activePoints = this.ActivePoints(retailLoyaltyRewardPoint);
                    RetailLoyaltyRewardPointAmountQty unvestedPoints = this.UnvestedPoints(retailLoyaltyRewardPoint);

                    if (issuedPoints != usedPoints + expiredPoints + activePoints + unvestedPoints)
                    {
                        RetailLoyaltyRewardPointID retailLoyaltyRewardPointID = retailLoyaltyRewardPoint.RewardPointId;
                        _line = strFmt('%1,%2\n', loyaltyCardNumber, retailLoyaltyRewardPointID);
                        textStreamIO.writeRaw(_line);
                    }
                }
                File::SendFileToUser(textStreamIO.getStream(), csvFileName);
            }
        }
        catch (Exception::Deadlock)
        {
            retry;
        }
        catch (Exception::UpdateConflict)
        {
            if (appl.ttsLevel() == 0)
            {
                if (xSession::currentRetryCount() >= #RetryNum)
                {
                    throw Exception::UpdateConflictNotRecovered;
                }
                else
                {
                    retry;
                }
            }
            else
            {
                throw Exception::UpdateConflict;
            }
        }
    }

    static void main(Args _args)
    {
        EYSLoyaltyCardPointsMisMatchBatchJob EYSLoyaltyCardPointsMisMatchBatchJob = EYSLoyaltyCardPointsMisMatchBatchJob::construct();

        if(EYSLoyaltyCardPointsMisMatchBatchJob.prompt())
        {
            EYSLoyaltyCardPointsMisMatchBatchJob.runOperation();
        }
    }

    public RetailLoyaltyRewardPointAmountQty IssuedPoints(RetailLoyaltyRewardPoint record)
    {
        return RetailLoyaltyCardRewardPointTrans::calcPoints(retailLoyaltyCard, record.recid, RetailLoyaltyPointStatusTypeBase::Issued);
    }

    public RetailLoyaltyRewardPointAmountQty UsedPoints(RetailLoyaltyRewardPoint record)
    {
        return RetailLoyaltyCardRewardPointTrans::calcPoints(retailLoyaltyCard, record.recid, RetailLoyaltyPointStatusTypeBase::Used);
    }

    public RetailLoyaltyRewardPointAmountQty ExpiredPoints(RetailLoyaltyRewardPoint record)
    {
        return RetailLoyaltyCardRewardPointTrans::calcPoints(retailLoyaltyCard, record.recid, RetailLoyaltyPointStatusTypeBase::Expired);
    }

    public RetailLoyaltyRewardPointAmountQty ActivePoints(RetailLoyaltyRewardPoint record)
    {
        return RetailLoyaltyCardRewardPointTrans::calcPoints(retailLoyaltyCard, record.recid, RetailLoyaltyPointStatusTypeBase::Active);
    }

    public RetailLoyaltyRewardPointAmountQty UnvestedPoints(RetailLoyaltyRewardPoint record)
    {
        return RetailLoyaltyCardRewardPointTrans::calcPoints(retailLoyaltyCard, record.recid, RetailLoyaltyPointStatusTypeBase::Unvested);
    }

    public str generateFileName()
    {
        utcdatetime currentDateTime = DateTimeUtil::getSystemDateTime();
        str formattedDateTime =  DateTimeUtil::toStr(currentDateTime);
        str fileName = strFmt('LoyaltyCardPointMisMatch_%1.csv', formattedDateTime);
        return fileName;
    }

}
```
